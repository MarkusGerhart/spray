grammar org.eclipselabs.spray.styles.Style with org.eclipse.xtext.xbase.Xbase

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate styles "http://www.eclipselabs.org/spray/styles/Style"

StyleContainer:
	styles+=Style*;

Style:
	{Style}
	"style" name=ID ("extends" superStyle=JvmTypeReference)? "{"
	   ("description" "=" description=STRING)?
	   layout=StyleLayout
	"}";

StyleLayout: 
    {StyleLayout}
    (
		("transparency"     "=" transparency=DOUBLE)? &
		("background-color" "=" background=ColorWithTransparency)? &
		("line-color"       "=" lineColor=ColorWithTransparency)? & 
        ("line-width"       "=" lineWidth=INT)? &
        ("line-style"       "=" lineStyle=LineStyle)? &
        ("font-name"        "=" fontName=STRING)? &
	    ("font-color"       "=" fontColor=Color)? &
	    ("font-size"        "=" fontSize=INT)? &
	    ("font-italic"      "=" fontItalic=YesNoBool)? &
	    ("font-bold"        "=" fontBold=YesNoBool)?
    )
;

ColorWithTransparency: Color | Transparent;
Color: RGBColor | ColorConstantRef;
RGBColor: "RGB" "(" red=INT "," green=INT "," blue=INT ")";
ColorConstantRef: value=ColorConstants;
Transparent: transparent?="transparent";
enum ColorConstants:
	WHITE="white" |
	LIGHT_LIGHT_GRAY="very-light-gray" |
	LIGHT_GRAY="light-gray" |
	GRAY="gray" |
	DARK_GRAY="dark-gray" |
	BLACK="black" |
	RED="red" |
	LIGHT_ORANGE="light-orange" |
	ORANGE="orange" |
	DARK_ORANGE="dark-orange" |
	YELLOW="yellow" |
	GREEN="green" |
	LIGHT_GREEN="light-green" |
	DARK_GREEN="dark-green" |
	CYAN="cyan" |
	LIGHT_BLUE="light-blue" |
	BLUE="blue" |
	DARK_BLUE="dark-blue";

enum LineStyle:
	SOLID="solid" |
	DOT="dot" |
	DASH="dash" |
	DASHDOT="dash-dot" |
	DASHDOTDOT="dash-dot-dot";

enum YesNoBool:
	YES="yes" | 
	NO="no";

DOUBLE returns ecore::EDouble: INT "." INT;
