module org.eclipselabs.spray.styles.generator.StyleGenerator

import org.eclipse.emf.mwe.utils.*

var srcGenDir = "src-gen"
var srcManDir = "src"
var clean = "true"
var modelPath
//var validate = "true"

Workflow {
    bean = StandaloneSetup {
        registerGeneratedEPackage = "org.eclipse.xtext.xbase.XbasePackage"
        registerGeneratedEPackage = "org.eclipselabs.spray.styles.StylesPackage"
        registerGenModelFile = "platform:/resource/org.eclipselabs.spray.styles/src-gen/org/eclipselabs/spray/styles/Style.genmodel"
        registerGenModelFile = "platform:/resource/org.eclipse.xtext.common.types/model/JavaVMTypes.genmodel"
		registerGenModelFile = "platform:/resource/org.eclipse.xtext.xbase/model/Xbase.genmodel"        
//        registerGeneratedEPackage = "org.eclipse.xtext.common.types.TypesPackage"
//        registerGenModelFile = "platform:/resource/org.eclipse.xtext.common.types/model/JavaVMTypes.genmodel"
    }

    component = org.eclipse.xtext.ecore.EcoreSupport{}

	component = org.eclipselabs.spray.xtext.util.ReaderExt {
		// lookup all resources on the classpath
		useJavaClassPath = true

		// or define search scope explicitly
		path = modelPath

		// this class will be generated by the xtext generator 
		register = org.eclipselabs.spray.styles.StyleStandaloneSetup {}
    	uriFilter = org.eclipse.xtext.mwe.NameBasedFilter {
    		regularExpression = ".*\\.style"
    	}
		loadResource = {
			slot = "model"
			uri = ".*\\.style" 
		}
	}

	component = org.eclipse.emf.mwe.core.container.IfComponent {
		cond = clean
		component = DirectoryCleaner {
			directory = srcGenDir
		}
	}

	component = org.eclipse.xtext.generator.GeneratorComponent {
		register =org.eclipselabs.spray.styles.StyleStandaloneSetup {}
		slot = 'model'
		outlet = {
			path = srcGenDir
		}
		outlet = {
		    outletName ="SRCMAN"
			path = srcManDir
		}
	}
}
