shape StartNode(java.lang.String precondition) 
	style org.eclipselabs.spray.styles.StartNodeStyle {
	size-min (width=100,height=50)
	size-max (width=200,height=50)
	stretching (horizontal=true,vertical=false)
	rectangle style org.eclipselabs.spray.styles.LabelRectangeStyle {
		position (x=0,y=0)
		size (width=100,height=20)
		text style org.eclipselabs.spray.styles.LabelRectangeStyle {
			position (x=0,y=0)
			size (width=100,height=20)
			align (horizontal=center,vertical=top)
			value = precondition
		}
	}
	ellipse {
		position (x=38,y=38)
		size (width=24,height=24)
		style (background-color=black)
	}
	anchor center
}

shape EndNode(java.lang.String postcondition) 
	style org.eclipselabs.spray.styles.EndNodeStyle {
	size-min (width=100,height=50)
	size-max (width=200,height=50)
	stretching (horizontal=true,vertical=false)
	ellipse {
		position (x=38,y=12)
		size (width=24,height=24)
		ellipse {
			position (x=6,y=6)
			size (width=12,height=12)
			style (background-color=black)
		}
	}
	rectangle style org.eclipselabs.spray.styles.LabelRectangeStyle {
		position (x=0,y=40)
		size (width=100,height=20)
		text {
			position (x=0,y=0)
			size (width=100,height=20)
			align (horizontal=center,vertical=bottom)
			value = postcondition
		}
	}
	anchor center
}

shape Node(java.lang.String name, java.lang.String desc) {
	rounded-rectangle {
		size (width=200,height=100)
		curve (width=2,height=2)
		text style org.eclipselabs.spray.styles.NodeNameStyle {
			position (x=25,y=5)
			size (width=150,height=20)
			align (horizontal=center,vertical=top)
			value = name
		}		
		line {
			point (x=0,y=27)
			point (x=200,y=27)
			style (line-width=2)
		}
		text style org.eclipselabs.spray.styles.NodeDescStyle {
			position (x=15,y=30)
			size (width=170,height=65)
			align (horizontal=left,vertical=top)
			value = desc
		}
	}
	anchor center
}

connection StateConnection 
	style org.eclipselabs.spray.styles.TransitionStyle {
	connection-type freeform
}